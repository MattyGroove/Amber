// Chapter Talks
IF
	GlobalLT("M#AmberChapterTalk","Locals",8)
	Global("Chapter","GLOBAL",%bg2_chapter_5%)
	GlobalLT("M#AmberMadForPhaere","Locals",1)
	AreaCheck("AR2100")
	!See("udsvir04")
	Global("HadSlayerDream","GLOBAL",1)
	OR(2)
	 Global("M#AmberMatch","GLOBAL",1)
	 Global("M#AmberMatch","GLOBAL",4)
	CombatCounter(0)
	See(Player1)
	InParty("M#Amber")
THEN
	RESPONSE #100
	SetGlobal("M#AmberChapterTalk","Locals",8)
	SetDialog("m#ambern")
	PlaySong(999997)
	StartDialogueNoSet(Player1) //Chapter talk 5.2 aka Underdark
END

//Love Talks
IF
	RealGlobalTimerExpired("M#AmberRomanceTime","GLOBAL")
	Global("M#AmberTalk","Locals",52)
	Global("M#AmberMatch","GLOBAL",1)
	!Global("Chapter","GLOBAL",%bg2_chapter_5%)
	!AreaType(DUNGEON)
	AreaType(OUTDOOR)
	AreaType(WEATHER)
	TimeOfDay(NIGHT)
	CombatCounter(0)
	See(Player1)
	InParty("M#Amber")
THEN
	RESPONSE #100
	IncrementGlobal("M#AmberTalk","Locals",1)
	SetDialog("M#Ambern")
	PlaySong(999997)
	StartDialogueNoSet(Player1) // Lovetalk 2 aka. Stars
END

IF
	RealGlobalTimerExpired("M#AmberRomanceTime","GLOBAL")
	Global("M#AmberTalk","Locals",58)
	GlobalGT("M#AmberLike","Locals",110)
	Global("M#AmberMatch","GLOBAL",1)
	OR(8)
	  AreaCheck("AR0313") //Sea's Bounty
	  AreaCheck("AR0406") //Copper Coronet
	  AreaCheck("AR0509") //Five Flagons 
	  AreaCheck("AR0522") //Five Flagons (Stronghold)
	  AreaCheck("AR0704") //Mithrest Inn 
	  AreaCheck("AR1105") //Umar Inn
	  AreaCheck("AR1602") //The Vulgar Monkey
	  AreaCheck("AR2010") //Vyatri's Pub in Trademeet
	CombatCounter(0)
	See(Player1)
	InParty("M#Amber")
THEN
	RESPONSE #100
	IncrementGlobal("M#AmberTalk","Locals",1)
	SetDialog("m#ambern")
	PlaySong(999997)
	StartDialogueNoSet(Player1) // Lovetalk 5 aka. Third Date
END

// Floating Lovetalks
IF
	Global("M#AmberDrowTalk","Locals",0)
	AreaCheck("AR2202") //Ust Natha Tavern
	Global("M#AmberMatch","GLOBAL",1)
	Global("M#AmberRomanceActive","GLOBAL",2) 
	GlobalLT("M#AmberMad","GLOBAL",1)
	GlobalLT("M#AmberMadForPhaere","Locals",1)
	CombatCounter(0)
	See(Player1)
	InParty("m#amber")
THEN
	RESPONSE #100
	SetGlobal("M#AmberDrowTalk","Locals",1)
	SetDialog("m#ambern")
	PlaySong(999997)
	StartDialogueNoSet(Player1) // Floating Lovetalk 3 aka. Resting in Ust Natha
END	

IF
	Global("M#BathTalk","GLOBAL",0)
	OR(5)
	  AreaCheck("AR1100") // Umar Hills
	  AreaCheck("AR1200") // Windspear Hills
	  AreaCheck("AR1700") // Small Teeth Pass
	  AreaCheck("AR1800") // North Forest
	  AreaCheck("AR1900") // Druid's Grove
	GlobalLT("M#AmberMad","GLOBAL",1)
	Global("M#AmberMatch","GLOBAL",1)
	Global("M#AmberRomanceActive","GLOBAL",2)
	CombatCounter(0)
	See(Player1)
	InParty("M#Amber")
THEN
	RESPONSE #100
	SetGlobal("M#BathTalk","GLOBAL",1)
	SetDialog("M#Ambern")
	ActionOverride(Player1,StartDialogue("Player1",Player1)) // Floating lovetalk 2 aka Bath
END